#!/usr/bin/perl -w
# kanopya-aggregator.pl - This is the main script to run kanopya monitoring data collect.

#    Copyright Â© 2011 Hedera Technology SAS
#    This program is free software: you can redistribute it and/or modify
#    it under the terms of the GNU Affero General Public License as
#    published by the Free Software Foundation, either version 3 of the
#    License, or (at your option) any later version.
#
#    This program is distributed in the hope that it will be useful,
#    but WITHOUT ANY WARRANTY; without even the implied warranty of
#    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#    GNU Affero General Public License for more details.
#
#    You should have received a copy of the GNU Affero General Public License
#    along with this program.  If not, see <http://www.gnu.org/licenses/>.

# Maintained by Dev Team of Hedera Technology <dev@hederatech.com>.
# Created 14 july 2010

=head1 NAME

kanopya-aggregator - Aggregator

=head1 SYNOPSIS	    

	$ ./kanopya-aggregator

=head1 DESCRIPTION

Aggregator is the main script to run indicator aggregation over group of hosts.

=head1 METHODS

=cut

use strict;
use warnings;

use BaseDB;
use General;
use Aggregator;
use Kanopya::Config;
use Log::Log4perl "get_logger";

my $dir = Kanopya::Config::getKanopyaDir();
Log::Log4perl->init($dir . '/conf/aggregator-log.conf');
my $log = get_logger("");

my $running = 1;

sub signalHandler {
	my $sig = shift;
	$log->info($sig." received : stopping main loop");
	$running = 0;
}

$SIG{TERM} = \&signalHandler;

sub runAggregator {
    my %args = @_;

    my $aggregator = Aggregator->new(service_providers => $args{service_providers});

    # enter in the main loop and continue while $$running is true
    $aggregator->run(\$running);
}

eval {
    # Firstly authenticate here, to get the list of service providers
    my $config = Kanopya::Config::get('aggregator');

    # Authenticate the daemon to the api.
    BaseDB->authenticate(login    => $config->{user}->{name},
                         password => $config->{user}->{password});

    # Infinite loop.
    # - If multiprocess is not set, run the aggregator normally
    # - If multiprocess is set, spawn one process for each service provider
    if ($config->{multiprocess}) {
        # Keep the processing service providers
        my $processing = {};
        while (1) {
            my @serviceproviders = Entity::ServiceProvider->search(hash => {
                                       service_provider_type_id => { not => undef }
                                   });

            my $pid;
            for my $serviceprovider (@serviceproviders) {
                if (not defined $processing->{$serviceprovider->id}) {
                    $processing->{$serviceprovider->id} = $serviceprovider;

                    # Run an aggregator in a child
                    $log->info("Run an aggregator for service <" . $serviceprovider->id . "> in a child process...");
                    $pid = fork();
                    if ($pid == 0) {
                        runAggregator(service_providers => [ $serviceprovider->id ]);
                        last;
                    }
                }
            };
            if (not defined $pid or $pid != 0) {
                sleep 30;
            }
        }
    }
    else {
        runAggregator();
    }
};
if ($@) {
	my $ex = $@;
	$log->error("error in aggregator instanciation: $ex");
	die "Catch error in Aggregator instanciation: $ex";
};

$log->info("kanopya-aggregator end.");


__END__

=head1 AUTHOR

Copyright (c) 2010 by Hedera Technology Dev Team (dev@hederatech.com). All rights reserved.
This program is free software; you can redistribute it and/or modify it under the same terms as Perl itself.

=cut
